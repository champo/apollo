repositories {
    flatDir {
        dirs "${project.projectDir}/libs"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.squareup.sqldelight'
apply plugin: 'kotlin-android'

apply from: "${project.rootDir}/linters/checkstyle/check-android.gradle"
apply from: "${project.rootDir}/linters/pmd/check-android.gradle"

// spotbugs is kinda broken for android projects
// it doesnt recognize what it has to check from android settings and needs a lot of hacking to work
// https://github.com/spotbugs/spotbugs-gradle-plugin/issues/90
//apply from: "${project.rootDir}/linters/findbugs/check-android.gradle"

/**
 * Returns the ip of the local network specified by the TESTING_LOCAL_NETWORK env var.
 */
def testingLocalIp() {
    def interfaceName = System.getenv("TESTING_LOCAL_NETWORK")?.trim()

    return NetworkInterface.getNetworkInterfaces().
            findAll { interfaceName && interfaceName.equalsIgnoreCase(it.displayName) }.
            collect { Collections.list it.getInetAddresses() }.
            flatten().
            find()?. // first or null
            getHostAddress()
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        // circle
        buildConfigField("String", "TESTING_LOCAL_IP", "\"${testingLocalIp()}\"")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        minified {
            debuggable true
            minifyEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
        htmlReport true
        lintConfig file("${project.rootDir}/linters/android-lint/config.xml")
    }

    testOptions {
        // https://www.veskoiliev.com/running-android-unit-instrumentation-tests-from-the-console/
        unitTests.all {
            testLogging {
                outputs.upToDateWhen {false}
                events "passed", "failed", "standardError"
                showCauses true
                showExceptions true
            }
        }
    }
}

ext {
    version_retrofit = '2.3.0'
    version_workmanager = '2.4.0'
}

def libwalletSources = fileTree("${project.rootDir}/libwallet") {
    include "**/*.go"
    exclude ".build/**"
    exclude "**/*_test.go"
}

task libwallet(type: Exec) {
    group = "build"
    description = "Build libwallet for Android bindings"

    ext {
        aar = "${project.projectDir}/libs/libwallet.aar"
        script = "${project.rootDir}/tools/libwallet-android.sh"
    }

    inputs.property("aar", aar)
    inputs.property("script", script)

    inputs.files(libwalletSources)
    inputs.file(script)

    outputs.file(aar)

    outputs.cacheIf { true }

    executable script
    args aar
}

preBuild.dependsOn "libwallet"

dependencies {
    api project(':common')
    implementation files('libs/libwallet.aar')

    // Android support:
    api 'androidx.appcompat:appcompat:1.2.0'

    implementation 'androidx.sqlite:sqlite-framework:2.0.1'

    // ReactiveX:
    api 'io.reactivex:rxjava:1.3.0'
    api 'io.reactivex:rxandroid:1.2.1'
    implementation 'com.github.akarnokd:rxjava2-interop:0.13.7'

    // Dependency injection:
    api 'com.google.dagger:dagger:2.27'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.27'

    compileOnly 'org.glassfish:javax.annotation:10.0-b28'

    // Logging:
    api 'com.jakewharton.timber:timber:4.5.1'

    // Money:
    api 'org.javamoney:moneta-bp:1.0'
    api 'org.zalando:jackson-datatype-money:0.12.0'

    // Firebase:
    api 'com.google.firebase:firebase-messaging:17.3.4'

    // WorkManager: (see https://github.com/muun/muun/issues/5895)
    // Guide: https://developer.android.com/topic/libraries/architecture/workmanager/migrating-fb
    api "androidx.work:work-runtime:$version_workmanager" // api as needs to be init at app.OnCreate

    // Debugging:
    api('com.google.firebase:firebase-crashlytics:17.2.1')
    // Analytics: Enhance Crashlytics experience (real time crash-free users + breadcrumbs)
    api 'com.google.firebase:firebase-analytics:17.5.0'

    api 'com.facebook.stetho:stetho:1.5.0'
    api 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    api 'com.facebook.stetho:stetho-timber:1.5.0@aar'

    // Storage:
    implementation 'com.squareup.sqlbrite3:sqlbrite:3.2.0'
    implementation 'com.google.auto.value:auto-value-annotations:1.6'
    annotationProcessor 'com.google.auto.value:auto-value:1.6'

    // Networking:
    implementation "com.squareup.retrofit2:retrofit:$version_retrofit"
    implementation "com.squareup.retrofit2:converter-jackson:$version_retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava:$version_retrofit"
    implementation "com.squareup.retrofit2:converter-scalars:$version_retrofit"
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'

    // Google APIs
    implementation 'com.google.android.gms:play-services-auth:18.1.0'
    implementation 'com.google.api-client:google-api-client-android:1.26.0'
    implementation 'com.google.apis:google-api-services-drive:v3-rev136-1.25.0'
    implementation 'com.google.http-client:google-http-client-gson:1.26.0'

    // Kotlin:
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$global_kotlin_version"

    // Testing:
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'

    testImplementation 'org.mockito:mockito-core:2.8.47'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"
    testImplementation 'org.assertj:assertj-core:3.8.0'
    testImplementation 'junit:junit:4.12'
    // Can't use Jake Wharton's threeten lib for test. For more info see:
    // https://github.com/JakeWharton/ThreeTenABP/issues/47
    testImplementation 'org.threeten:threetenbp:1.3.5'
    testImplementation 'br.com.six2six:fixture-factory:3.1.0'
    testImplementation 'net.sourceforge.streamsupport:streamsupport:1.5.5'
    testImplementation 'com.github.tomakehurst:wiremock-standalone:2.6.0'

    testImplementation files('libs/libwallet.aar')
}
